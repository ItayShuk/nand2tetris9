class Snake{
    // represents the head of the snake
    field Points head;
    field Array body;
    field int size;
    field Points direction;
    field int snake_rec;


    /** constructor of the Snake class, given default direction and size*/
    constructor Snake new(){
        let head = Points.new(250, 125);
        let body = Array.new(2);
        let body[0] = head;
        let body[1] = Points.new(258, 125);
        let size = 2;
        let snake_rec = 8;
        let direction = Points.new(1,0);
        return this;
    }
    /** increases the snake size in the case the snake ate food */
    method void incSize(){
        var Array new_body;
        var Points new_head;
        var int i;
        let i = 0;
        let new_body = Array.new(size+1);
        let new_head = Points.new((head.getX() + direction.getX()), (head.getY() + direction.getY()));
        let new_body[0] = new_head;
        while (i < size)
         {
            let new_body[i+1] = body[i];
            let i = i + 1;
         }
        let body = new_body;
        let size = size+1;
        let head = body[0];
        return;
    }

    /** method that moves the snake at the current direction*/
    method void move(){
        var Points new_head;
        var Array new_body;
        var int i;
        var Points tail;
        var int tailX;
        var int tailY;
        let tail = body[size-1];
        let tailX = tail.getX();
        let tailY = tail.getY();
        let new_body =  Array.new(size);
        let new_head = Points.new((head.getX() + direction.getX()), (head.getY() + direction.getY()));
        let i = 0;
        let new_body[0] = new_head;
        while (i<(size-1)){
            let new_body[i+1] = body[i];
            let i = i + 1;
        }

        let body = new_body;
        let head = new_body[0];
        do eraseTail(tailX, tailY);
        return;
    }
    /** method that checks if the snake had a collision with himself */
    method boolean collision(){
        var int i;
        var Points current;
        let i = 1;
        while (i<size){
            let current = body[i];
            if ((head.getX() = current.getX()) & (head.getY() = current.getY()))
            {
                return false;
            }
            let i = i + 1;
        }
        return true;
    }
    /** returns the head of the snake */
    method Points getHead(){
        return head;
    }
    /** returns the body of the snake */
    method Array get_body(){
        return body;
    }
    /** returns the direction of the snake */
    method Points getDirection(){
        return direction;
    }
    /** returns the size of the snake */
    method int get_size(){
        return size;
    }
    /** disposes of the snakes body */
    method void disposeBody(){


        do Memory.deAlloc(body);
        do direction.dispose();
        do Memory.deAlloc(this);

        return;
    }

    /** draws the body of the snake on the screen*/
    method void draw(){
        var int i;
        var Points current;
        let i = 0;
        while (i<size){
            let current = body[i];
            do Screen.setColor(true);
            do Screen.drawRectangle(current.getX(), current.getY(), (current.getX() + snake_rec), (current.getY() + snake_rec));
            let i = i+1;
        }
        return;
    }

    method Points body_ind(int i){
        return body[i];
    }


    method void eraseTail(int x, int y)
    {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, (x + snake_rec), (y + snake_rec));
        return;
    }
}